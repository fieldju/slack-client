group 'com.fieldju'
version '1.0.1'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.squareup.okhttp', name: 'mockwebserver', version: '2.7.5'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    // copy the needed props to the int tests
    systemProperties = [
            arn: System.getProperty('arn'),
            bucketName: System.getProperty('bucketName'),
            logKey: System.getProperty('logKey')
    ]
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    testLogging {
        events "passed", "skipped", "failed"
    }
}

def bintrayUser = project.hasProperty('bintrayUser') ? project.bintrayUser
        : System.getenv('BINTRAY_USER') ?: ''

def bintrayKey = project.hasProperty('bintrayKey') ? project.bintrayKey
        : System.getenv('BINTRAY_KEY') ?: ''

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'slack-client'
        userOrg = 'fieldju'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/fieldju/slack-client'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}